plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'groovy'
    id 'com.palantir.docker' version '0.34.0'
}

group = 'pl.programistazacny'
version = '2.0.0-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.mapstruct:mapstruct:1.5.2.Final'
    implementation 'com.opencsv:opencsv:5.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2:2.1.214'
    //implementation 'org.zalando:logbook-spring-boot-starter:2.6.2' conflict with H2 console
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.304')
    implementation 'io.github.boostchicken:spring-data-dynamodb:5.2.5'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.spockframework:spock-bom:2.2-groovy-4.0')
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-spring'
    testImplementation 'org.apache.groovy:groovy-all:4.0.5'
    testImplementation 'com.tngtech.archunit:archunit:0.23.1'
    testImplementation platform('org.testcontainers:testcontainers-bom:1.17.3')
    testImplementation('org.testcontainers:spock')
}

test {
    useJUnitPlatform()

    afterTest { desc, result ->
        logger.quiet "Executing test \"${desc.name}\" [${desc.className}] with result: ${result.resultType} and took: ${result.endTime - result.startTime}ms"
    }
}

docker {
    def dockerName = "$project.group/$project.name".toLowerCase()
    def dockerTag = project.version.toLowerCase()
    def jarFileName = bootJar.archiveFileName.get()

    name dockerName
    tag 'latest', "$dockerName:$dockerTag"
    files project.libsDirectory
    buildArgs([
            jarFileName: jarFileName
    ])
    noCache true
}
